" Ryan Kuczka

" Basic Setup:
" ============
set nocompatible
filetype plugin indent on

" Load Plugins: {{{1
" =============
call plug#begin()
Plug '/repos/vim-pyfold'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-rhubarb'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-abolish'
Plug 'tpope/vim-unimpaired'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-vinegar'
Plug 'tpope/vim-eunuch'
Plug 'tpope/vim-characterize'
Plug 'takac/vim-commandcaps'
Plug 'bling/vim-airline'
Plug 'Valloric/YouCompleteMe'
Plug 'mbbill/undotree', { 'on': 'UndotreeToggle' }
Plug 'Yggdroot/indentLine'
Plug 'suan/vim-instant-markdown', { 'for': 'markdown' }
Plug 'scrooloose/syntastic'
Plug 'scrooloose/nerdtree'
Plug 'majutsushi/tagbar'
Plug 'ctrlpvim/ctrlp.vim'
Plug 'JazzCore/ctrlp-cmatcher'
Plug 'Lokaltog/vim-easymotion'
Plug 'airblade/vim-gitgutter'
Plug 'wellle/targets.vim'
Plug 'haya14busa/incsearch.vim'
Plug 'jlanzarotta/bufexplorer'
Plug 'guns/xterm-color-table.vim'
Plug 'ap/vim-css-color'
Plug 'Konfekt/FastFold'
Plug 'PeterRincker/vim-argumentative'
if exists('$TMUX')
  Plug 'christoomey/vim-tmux-navigator'
  Plug 'tmux-plugins/vim-tmux-focus-events'
  Plug 'tmux-plugins/vim-tmux'
endif
" Color Schemes {{{2
Plug 'jonathanfilip/vim-lucius'
Plug 'altercation/vim-colors-solarized'
Plug 'nanotech/jellybeans.vim'
Plug 'w0ng/vim-hybrid'
Plug 'whatyouhide/vim-gotham'
" Syntax Files {{{2
Plug 'hdima/python-syntax'
Plug 'evanmiller/nginx-vim-syntax'
Plug 'groenewege/vim-less'
Plug 'hail2u/vim-css3-syntax'
Plug 'othree/html5.vim'
Plug 'othree/yajs.vim'
Plug 'othree/javascript-libraries-syntax.vim'
Plug 'gavocanov/vim-js-indent'
Plug 'ingydotnet/yaml-vim'
Plug 'octol/vim-cpp-enhanced-highlight'
call plug#end() " }}}1

let mapleader = ','
let maplocalleader = ','

" Configure Plugins: {{{1
" ==================
" Fugitive {{{2
nnoremap <Leader>gs <C-W>o:Gstatus<CR>
" open the current file on github
nnoremap <Leader>gb :.Gbrowse<CR>
vnoremap <Leader>gb :Gbrowse<CR>

" CommandCaps {{{2
let g:commandcaps_override = 1

" YouCompleteMe {{{2
let g:ycm_complete_in_comments = 1
let g:ycm_collect_identifiers_from_tags_files = 1
let g:ycm_collect_identifiers_from_comments_and_strings = 1

" Airline {{{2
let g:airline_powerline_fonts=1
let g:airline_theme='ryansolarized'
let g:airline#extensions#hunks#non_zero_only = 1
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#tabline#show_buffers = 0
let g:airline#extensions#tabline#fnamemod = ':t'

" Undotree {{{2
nnoremap <Leader>u :UndotreeToggle<CR>

" IndentLine {{{2
let g:indentLine_char = '┊'
let g:indentLine_setColors = 0

" Instant Markdown {{{2
let g:instant_markdown_slow = 1

" Syntastic {{{2
nnoremap <C-S> :SyntasticCheck<CR>
let g:syntastic_mode_map = {'mode': 'passive'}
let g:syntastic_auto_loc_list=1
let g:syntastic_check_on_open=1
let g:syntastic_python_checkers=["flake8"]
let g:syntastic_python_flake8_args='--ignore=E501,E128,E261,E262'
let g:syntastic_javascript_checkers=["jshint"]
let g:syntastic_javascript_jshint_args='--config /sites/ycharts/confs/developers/jshint_conf.json'
let g:syntastic_sh_checkers=["shellcheck"]
let g:syntastic_json_checkers=["jsonlint"]

" CtrlP {{{2
let g:ctrlp_working_path_mode='wra'
let g:ctrlp_max_files=0
let g:ctrlp_user_command=['.git', 'cd %s && git ls-files . -co --exclude-standard', 'ag %s -l --nocolor -g ""']
let g:ctrlp_match_func={'match' : 'matcher#cmatch'}
let g:ctrlp_extensions=['buffertag']
let g:ctrlp_mruf_relative=1

" EasyMotion {{{2
let g:EasyMotion_do_mapping = 0
map <Leader>f <Plug>(easymotion-f)
map <Leader>F <Plug>(easymotion-F)
map <Leader>n <Plug>(easymotion-vim-n)
map <Leader>N <Plug>(easymotion-vim-N)
map <Leader>s <Plug>(easymotion-s)
map <Leader>; <Plug>(easymotion-next)
map <Leader>. <Plug>(easymotion-repeat)
map <Leader>j <Plug>(easymotion-j)
map <Leader>k <Plug>(easymotion-k)

" GitGutter {{{2
let g:gitgutter_realtime = 0
let g:gitgutter_eager = 0

" IncSearch {{{2
map / <Plug>(incsearch-forward)
map ? <Plug>(incsearch-backward)
map g/ <Plug>(incsearch-stay)
" No Highlighting
let g:incsearch#auto_nohlsearch = 1
map n <Plug>(incsearch-nohl-n)
map N <Plug>(incsearch-nohl-N)
map * <Plug>(incsearch-nohl-*)N
map # <Plug>(incsearch-nohl-#)N
map g* <Plug>(incsearch-nohl-g*)N
map g# <Plug>(incsearch-nohl-g#)N
" n always forward / N always backward
let g:incsearch#consistent_n_direction = 1

" BufExplorer {{{2
let g:bufExplorerDisableDefaultKeyMapping = 1
let g:bufExplorerSplitBelow = 1
let g:bufExplorerSplitHorzSize = 15

nnoremap <silent> <Leader>b :BufExplorerHorizontalSplit<CR>

" TagBar {{{2
let g:tagbar_left = 1
nnoremap <silent> <Leader>o :TagbarToggle<CR>

" NERDTree {{{2
nnoremap <silent> <Leader>d :NERDTreeToggle<CR>
nnoremap <silent> <Leader>l :NERDTreeFind<CR>
let NERDTreeRespectWildIgnore = 1

" Argumentative {{{2
let g:argumentative_no_mappings = 1
nmap [, <Plug>Argumentative_Prev
nmap ], <Plug>Argumentative_Next
xmap [, <Plug>Argumentative_XPrev
xmap ], <Plug>Argumentative_XNext
nmap <, <Plug>Argumentative_MoveLeft
nmap >, <Plug>Argumentative_MoveRight

" Python Syntax {{{2
let g:python_version_2 = 1
let g:python_highlight_all = 1
let g:python_highlight_space_errors = 0

" Javascript Syntax {{{2
let g:javascript_ignore_javaScriptdoc = 1
let g:used_javascript_libs = 'angularjs,underscore'

" Standard Options: {{{1
" =================
" Syntax Highlighting {{{2
syntax on

set t_Co=256       " sets the number of colors to 256
set background=dark
colorscheme solarized

if g:colors_name == 'solarized'
  hi Folded cterm=bold gui=bold
  hi Error ctermfg=12 ctermbg=15 guifg=#002b36 guibg=#dc322f
  hi GitGutterAdd ctermfg=2 ctermbg=0 guifg=#719e07 guibg=#073642
  hi GitGutterDelete ctermfg=1 ctermbg=0 guifg=#dc322f guibg=#073642
  hi GitGutterChange ctermfg=3 ctermbg=0 guifg=#b58900 guibg=#073642
  hi link GitGutterChangeDelete GitGutterChange
  if &background == 'light'
    hi GitGutterAdd ctermbg=7 guibg=#eee8d5
    hi GitGutterDelete ctermbg=7 guibg=#eee8d5
    hi GitGutterChange ctermbg=7 guibg=#eee8d5
  endif
elseif g:colors_name == 'hybrid' || g:colors_name == 'hybrid-light'
  hi Conceal ctermbg=NONE guibg=NONE
  hi Search ctermfg=0
  hi DiffDelete ctermfg=0
elseif g:colors_name == 'lucius'
  hi Conceal ctermbg=NONE guibg=NONE
  hi SignColumn ctermbg=238 guibg=#444444
  hi LineNr ctermbg=238 guibg=#444444
endif

" Window Options {{{2
set number
set nowrap         " disables word wrapping
set scrolloff=3    " ensures that at least 3 lines of context are always visible
set laststatus=2
set noshowmode
set noruler        " shows line,col in bottom right
set showcmd        " shows the partial command in the lower right
set lazyredraw     " don't redraw window while executing macros/etc.
" turns tabs into , shows  for text extending beyond window
" and  for text extending left of window
set list listchars=tab:,extends:,precedes:

set timeout timeoutlen=1000 ttimeoutlen=10

" Turn off bell and visualbell
set visualbell t_vb=

" Set terminal/mouse support
set t_RV=
if !has('nvim')
  set ttymouse=xterm2
endif
set mouse=a

" Sets GUI Font and options
if has('gui_running')
  set guifont=Input\ Mono\ Compressed:h14
  set guioptions=gmtc
else
  " Sets cursor to a bar in insert mode
  if exists('$TMUX')
    let &t_SI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=1\x7\<Esc>\\"
    let &t_EI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=0\x7\<Esc>\\"
  else
    let &t_SI = "\<Esc>]50;CursorShape=1\x7"
    let &t_EI = "\<Esc>]50;CursorShape=0\x7"
  endif
endif
" Indentation {{{2
set tabstop=4      " sets the length of the tab to 4 spaces
set shiftwidth=4   " sets the (auto)indent to 4 spaces
set softtabstop=4  " sets number of spaces to insert for a <Tab>
set smarttab       " a <Tab> will insert 'shiftwidth' spaces
set expandtab      " a <Tab> is expanded to spaces in Insert mode
set autoindent     " auto indents
set shiftround     " force indentation to multiple of shiftwidth

if has('linebreak')
  set linebreak      " break wrapped lines at normal characters instead of last
  let &showbreak = '↪ ' " start wrapped lines with this char
  if has('breakindent')
    set breakindent    " make wrapped lines have same indentation
  endif
endif
" Command Line {{{2
set wildmode=longest,list,full " specifies that pressing <Tab> will complete to longest common substring, then a list of options, then cycle through all options
set wildmenu       " command line completion will show a menu of matches
set wildignore=*.pyc " patterns to ignore for file name completion
" Folding {{{2
set foldmethod=marker " allows setting a marker to specify where a fold occurs
set foldmarker={{{,}}} " sets the fold marker to {{{ to start and }}} to end
set foldlevel=99
" Search {{{2
set incsearch      " highlights as you search
set hlsearch       " highlights search results
set ignorecase     " ignores case for search
set smartcase      " overrides ignorecase if search pattern has upper case letters
set gdefault       " defaults to the `g` flag for :s substitutions

set grepprg=ag\ --vimgrep
set grepformat=%f:%l:%c:%m

" Files/Buffers {{{2
set hidden         " doesn't unload a buffer when it is no longer in a window
set undofile
set undodir=~/.vim/undodir// " store undo files here
set directory=~/.vim/swapfiles// " store swap (.swp) files here
set backupdir=~/.vim/backups// " store backup files (~) here

" Etc {{{2
set backspace=eol,indent,start
set completeopt=menuone " show completion menu even if only one match
set eol            " ensures the last line in the file has an end of line
set virtualedit=block " only allows the cursor to go where there is no character in Visual Block mode

set autoread       " when a file is changed by a program other than vim while a buffer is open, automatically reload it
set nostartofline  " makes it so G, gg, <C-D>, etc. don't move cursor to start of the line
set modeline       " Allow for modelines

set diffopt=filler,vertical " add filler lines and always use vertical
set nojoinspaces   " only add one space when joining lines

set sessionoptions-=options " don't save options in sessions, it gets wonky

if exists('&macmeta')
  set macmeta
endif

" Neovim Options {{{2
if has('nvim')
  " Bar cursor in insert mode
  let $NVIM_TUI_ENABLE_CURSOR_SHAPE=1

  " Terminal scrollback size
  let g:terminal_scrollback_buffer_size = 100000

  " Insert mode when entering terminals
  au WinEnter term://* call feedkeys('i')
endif

" Useful Mappings: {{{1
" ================
" Movement {{{2
" Make j and k work better with wrapped lines
noremap j gj
noremap k gk
" Add the <C-A> and <C-E> commands to insert/command modes
inoremap <C-A> <C-O>^
inoremap <C-E> <C-O>$
cnoremap <C-A> <Home>

" Window Movement
nnoremap <C-H> <C-W>h
nnoremap <C-J> <C-W>j
nnoremap <C-K> <C-W>k
nnoremap <C-L> <C-W>l

" Shortcuts {{{2
" Maps Y to y$ to work like D and C
nnoremap Y y$
" Make Q run the q macro instead of Ex mode
nnoremap Q @q
" Quick switch between alternate buffers
nnoremap <Leader><Leader><Space> <C-^>
" Quick re-select pasted text
nnoremap <expr> gp '`[' . strpart(getregtype(), 0, 1) . '`]'

" Syntax Debugging {{{2
" Allows looking up the syntax under the cursor
function! ShowSynStack()
  for id in synstack(line("."), col("."))
    echo synIDattr(id, "name")
  endfor
endfunction
nnoremap <F9> :call ShowSynStack()<CR>
nnoremap <F10> :echo "hi<" .synIDattr(synID(line('.'),col('.'),1),'name'). '> trans<'
            \ .synIDattr(synID(line('.'),col('.'),0),'name'). '> lo<'
            \ .synIDattr(synIDtrans(synID(line('.'),col('.'),1)),'name'). '>'<CR>

" Folds {{{2
nnoremap zr zr:echo &foldlevel<CR>
nnoremap zm zm:echo &foldlevel<CR>
nnoremap zR zR:echo &foldlevel<CR>
nnoremap zM zM:echo &foldlevel<CR>

" Filetype Overrides: {{{1
" ===================
augroup FileTypeConfs
  autocmd!
  autocmd BufRead,BufNewFile *nginx* set filetype=nginx
  autocmd BufRead,BufNewFile *cron.txt set filetype=crontab
  autocmd BufRead,BufNewFile *.html set filetype=htmldjango
  autocmd BufRead,BufNewFile *.tpl set filetype=htmldjango
  autocmd BufRead,BufNewFile *.md set filetype=markdown
  autocmd BufRead,BufNewFile *.sh,zshrc,*deployment_notes.txt let b:is_bash=1 | set filetype=sh
  autocmd BufRead,BufNewFile *requirements*.txt set filetype=config
augroup END " }}}1

" Miscellaneous Functions And Commands:
" =====================================
" Debug Statements: {{{1
" =================
" Insert debugger/ipdb statement
nnoremap <silent> <Leader><C-I> :call InsertDebugTrace()<CR>
function! InsertDebugTrace()
  if (&ft == 'python')
    normal! Oimport ipdb; ipdb.set_trace()
  elseif (&ft == 'javascript')
    normal! Odebugger;
  endif
endfunction

" Scratch Buffers: {{{1
" ================
function! ScratchBuf(...)
  if a:0 == 0
    let ft = &filetype
  else
    let ft = a:1
  endif

  new
  setlocal buftype=nofile
  setlocal bufhidden=hide
  setlocal noswapfile
  setlocal buflisted
  exec 'setlocal filetype='.ft
endfunction
command! -nargs=? -complete=filetype Scratch call ScratchBuf(<f-args>)

" Trailing Whitespace: {{{1
" ====================
hi link TrailingWhitespace Error
match TrailingWhitespace /\s\+$/
let s:trailingwhitespace_ignore_filetypes = ['gitcommit', 'unite', 'vimfiler', 'vim-plug']

function! s:match_ts(insert)
  if &modifiable && index(s:trailingwhitespace_ignore_filetypes, &ft) == -1
    if a:insert
      match TrailingWhitespace '\s\+\%#\@<!$'
    else
      match TrailingWhitespace '\s\+$'
    endif
  else
    call clearmatches()
  endif
endfunction

function! s:block_write_ts()
  if ! v:cmdbang && search('\s\+$', 'cnw')
        \&& index(s:trailingwhitespace_ignore_filetypes, &ft) == -1
    throw 'TrailingWhitespace: Aborting write due to trailing whitespace.'
  endif
endfunction

function! DeleteTS()
  let l:cursor_pos = getpos('.')
  keepjumps keeppatterns %s/\s\+$//e
  call setpos('.', l:cursor_pos)
endfunction

augroup TrailingWhitespace
  autocmd!
  autocmd BufWinEnter,InsertLeave * call s:match_ts(0)
  autocmd InsertEnter * call s:match_ts(1)
  autocmd BufWinLeave * call clearmatches()
  autocmd BufWritePre * try | call s:block_write_ts() | catch /^TrailingWhitespace:/ | echoerr substitute(v:exception, '^\CTrailingWhitespace:\s*', '', '') | endtry
augroup END

nnoremap <silent> <Leader>d$ :call DeleteTS()<CR>

" Prettify: {{{1
function! Prettify()
  if &ft == 'xml'
    exe '%!xmllint --format -'
  elseif &ft == 'json'
    exe '%!python -m json.tool'
  endif
endfunction
command! Prettify call Prettify()

" Ag: {{{1
function! Ag(...)
  if a:0 == 0
    execute 'grep! '.substitute(getreg('/'), '\\<\|\\>', '\\\\b', 'g')
  else
    execute 'grep! '.join(a:000, ' ')
  endif
  botright copen
endfunction
command! -nargs=* Ag call Ag(<f-args>)

" NewTab: {{{1
function! NewTab(dir)
  tabnew
  if a:dir != ''
    execute 'lcd '.a:dir
  endif
endfunction
command! -complete=dir -nargs=1 NewTab call NewTab(<q-args>)
" }}}1

" try to source local vimrc
if filereadable(glob("~/.vimrc.local"))
  source ~/.vimrc.local
endif

" vim:foldlevel=0
